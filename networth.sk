function getNW(p: player) :: number:
    set {-nw::ckelp_item} to a dried kelp block
    enchant {-nw::ckelp_item} with mending 10

    set {_nw} to (the amount of sea pickles in {_p}'s inventory) * 2048 
    add (the amount of sea pickles in {_p}'s ender chest) * 2048 to {_nw}
    add (the amount of dried kelp blocks in {_p}'s inventory) * 1028 to {_nw}
    add (the amount of dried kelp blocks in {_p}'s ender chest) * 1028 to {_nw}
    add (the amount of {-nw::ckelp_item} in {_p}'s inventory) * 3068 to {_nw} # Set to 3068, as 1024 is added earlier
    add (the amount of {-nw::ckelp_item} in {_p}'s ender chest) * 3068 to {_nw} # ^
    
    return {_nw}

function getNWRank(p: player) :: number: # Returns 0 for none, 1 for aristocrat, 2 for huge aristocrat, and 3 for ultimate aristocrat
    set {_nw} to getNW({_p})
    if {_nw} >= 1000000:
        return 3
    else if {_nw} >= 500000:
        return 2
    else if {_nw} >= 250000:
        return 1
    else:
        return 0

command /networth [<text>]:
    # Calculates the player's networth, from the player's inventory / ender chest
    aliases: /networth, /nw, /worth
    executable by: players
    usage: "/nw"
    description: "Calculates a player's networth"
    permission: msd.command.nw 
    permission message: "&r&fYou do not have permission to run this command."
    trigger:
        set {_nw} to getNW(player)
        if arg-1 contains "info" or "-i":
            send ""
            send " &b&nAbout&f &3&nNetworth"
            send ""
            send " &3&l» &bThe networth Command counts the amount of&3:"
            send " &3┌ &2Kelp&b (Equal to &81024 NW&b)&3," 
            send " &3├ &3Sea Pickles&b (Equal to &82048 NW&b)&3,"
            send " &3├ &2Compressed Kelp&b (Equal to &84096 NW&b)&3."
            send " &3└─ &bIn your inventory and ender chest&3."
            send " &3&l» &r&bNetworth is based in Netherite Ingots&3."
            send " &3&l» &r&bIt &c&ndoes not&b include anything stored within a &dshulker box&3."
            send " &3&l» &r&bYou can do &3/nw&b, &3/networth&b, or &3/worth&b to get your networth&3."
            send ""
        else:
            send ""
            send "&bYour networth is &8%{_nw}%&b."
            send ""
            send "&3/nw info &bfor more information."
            send "&3/nwr &bto recieve a networth rank."
            send ""

command /nwr [<text>]:
    aliases: /networth-rank, /nw-rank, /nwrank
    trigger:
        set {_p} to player
        set {_uuid} to {_p}'s uuid
        set {ranks::nw.%uuid of player%} to getNWRank({_p})
        if arg-1 contains "confirm":
            set {_nw} to getNW({_p})
            if {ranks::nw.%uuid of player%} is 3:
                send "&bYour networth is &8%{_nw}%&b. As such, you have been granted the &5Ultimate Aristocrat rank&b."
                command "/lp user %{_p}% parent set ultimate-aristocrat"
            else if {ranks::nw.%uuid of player%} is 2:
                send "&bYour networth is &8%{_nw}%&b. As such, you have been granted the &aHuge Aristocrat rank&b."
                command "/lp user %{_p}% parent set huge-aristocrat"
            else if {ranks::nw.%uuid of player%} is 1:
                send "&bYour networth is &8%{_nw}%&b. As such, you have been granted the &bAristocrat rank&b."
                command "/lp user %{_p}% parent set aristocrat"
            else:
                send "&bYour networth is &8%{_nw}%&b. As such, your rank has remained unchanged."

        else:
            send "&cThis command will override your current rank."
            send "&bIf you would like to proceed, run &3/nwr confirm"