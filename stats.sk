variables:
	{kills::players::%player%} = 0
	{nw::players::%player%} = 0

	{k-ranks::murderer::req} = 500
	{k-ranks::genocidal-maniac::req} = 1000
	{k-ranks::sadistic::req} = 2500
	{k-ranks::demon::req} = 5000
	{k-ranks::reaper::req} = 7500
	{k-ranks::blood-god::req} = 10000

	{nw-ranks::aristocrat::req} = 250000
	{nw-ranks::huge-aristocrat::req} =  500000
	{nw-ranks::ultimate-aristocrat::req} = 1000000
	# {staff_ranks::*} = "owner", "co-owner", "head-mod", "senior-mod", "mod", "trialmod", "trainee-mod", "op", "trial-builder", "builder", "head-builder"


options:
	kelp_value: 1024 # Compressed Kelp is 4x this value
	pickle_value: 2048
	credit_value: 4096

	do-logging: false # Whether or not to log nw, rank and kill changes. Default: true

function logc(msg: text): # Basic console logging
	if {@do-logging} is true:
		send "[stats.sk] %{_msg}%" to console

command /define_nw_item <text>: # ONLY NEEDS TO BE USED ONCE, WHEN THE SKRIPT IS FIRST INSTATED
	permission: skript.nwadmin
	permission message: &cInsufficient Permissions.
	usage: /define_nw_item <credit | ckelp | kelp> while holding the item
	trigger:
		if arg-1 contains "cre":
			set {nw::items::credit} to player's tool
			send "&3Defined &6Credit Item&3 as &3%{nw::items::credit}%"
			logc("Defined credit item as %{nw::items::credit}%")
			stop
		else if arg-1 contains "ckelp" or "compressed kelp" or "comp kelp":
			set {nw::items::ckelp} to player's tool
			send "&3Defined &6Compressed Kelp Item&3 as &3%{nw::items::ckelp}%"
			logc("Defined compressed kelp item as %{nw::items::ckelp}%")
			stop
		# else if arg-1 is "kelp":
		# 	set {nw::items::kelp} to player's tool
		# 	send "&3Defined &6Kelp Item&3 as &3%{nw::items::kelp}%"
		send "&cThat item is not supported."


### KILLS ### 

on death of player:
	attacker is player
	if attacker is victim:
		stop
	else:
		add 1 to {kills::players::%attacker%}
	
command /kills [<offline player>]:
	trigger:
		if arg-1 is not set:
			if command sender is not console:
				message "You have %{kills::players::%player%}% kills in total."
			else:
				message "&cA console does not have a kill count."
		else if player has permission "skript.getkills":
			message "%arg-1% has &6%{kills::players::%arg-1%}%&r kills in total."
		else:
			message "&cYou do not have permission to view the kills of other players."

command /modifykills <offline player> <text> <number>:
	aliases: /changekills, /ck, /mk
	usage: /ck <target> <+ | -> <amount>
	permission: skript.modifykills
	permission message: &cInsufficient Permissions.
	trigger:
		if arg-2 contains "add" or "+" or "sum":
			add arg-3 to {kills::players::%arg-1%}
			logc("%player%: + %arg-3% kills to %arg-1%. Now %{kills::players::%arg-1%}%")
		else if arg-2 contains "subtract" or "sub" or "minus" or "-" or "difference" or "diff":
			subtract arg-3 from {kills::players::%arg-1%}
			logc("%player%: - %arg-3% kills to %arg-1%. Now %{kills::players::%arg-1%}%")
		else:
			send "&cOne of your arguments is invalid. Please try again."
		send "The player &b%arg-1%&r now has &6%{kills::players::%arg-1%}%&r kills total" to player

command /checkkills:
	permission: skript.checkkills
	permission message: &cInsufficient Permissions.
	trigger:
		loop all players:
			send "The player &b%loop-player%&r has &6%{kills::players::%loop-player%}%&r kills in total." to player

on player join:
	set {_r} to (the luckperms group of player)
	if {kills-updated::*} does not contain player:
		add {kill counter.%player%.kills_total} to {kills::players::%player%}
		set {kill counter.%player%.kills_total} to 0
		add player to {kills-updated::*}
		
	if {_r} is "ultimate-aristocrat":
		if {nw::players::%player%} is less than {nw-ranks::ultimate-aristocrat::req}:
			removeRank(player)
			send "&cYou did not meet the requirements for your previously equipped rank (Ultimate Aristocrat)." to player
			send "&cUse /cv to get more net worth."
			logc("Removed ult-aristocrat from %player%")
	else if {_r} is "huge-aristocrat":
		if {nw::players::%player%} is less than {nw-ranks::huge-aristocrat::req}:
			removeRank(player)
			send "&cYou did not meet the requirements for your previously equipped rank (Huge Aristocrat)." to player
			send "&cUse /cv to get more net worth."
			logc("Removed huge-aristocrat from %player%")
	else if {_r} is "aristocrat":
		if {nw::players::%player%} is less than {nw-ranks::aristocrat::req}:
			removeRank(player)
			send "&cYou did not meet the requirements for your previously equipped rank (Aristocrat)." to player
			send "&cUse /cv to get more net worth."
			logc("Removed ult-aristocrat from %player%")

### NETWORTH ###
function getInventoryValue(p: player) :: number:

	set {_nw} to (the amount of sea pickles in {_p}'s inventory) * {@pickle_value} 
	add (the amount of dried kelp blocks in {_p}'s inventory) * {@kelp_value} to {_nw}
	add (the amount of {nw::items::ckelp} in {_p}'s inventory) * ({@kelp_value} * 3) to {_nw} # Set to *3, as the other 1 is added earlier
	add (the amount of {nw::items::credit} in {_p}'s inventory) * {@credit_value} to {_nw}

	return {_nw}

function removeItems(p: player, a: number, i: item, t: boolean):
	set {_a_inv} to the amount of {_i} in {_p}'s inventory
	if {_a_inv} is less than {_a}:
		send " &c&l» &r&3You do not have enough of this item." to {_p}
	else:
		remove {_a} of {_i} from {_p}'s inventory
		if {_i} is {nw::items::ckelp}:
			add {_a} * {@kelp_value} * 4 to {_nw}
		else if {_i} is {nw::items::credit}:
			add {_a} * {@credit_value} to {_nw}
		else if {_i} is a dried kelp block:
			add {_a} * {@kelp_value} to {_nw}
		else if {_i} is a sea pickle:
			add {_a} * {@pickle_value} to {_nw}
		else:
			send "&cInvalid Item. If you get this error, please report it to a staff member." to {_p}
		add {_nw} to {nw::players::%{_p}%}
		logc("Removed %{_a}% of %{_i}% from %{_p}% for %{_nw}% nw. Now %{nw::players::%{_p}%}%")
		if {_t} is true:
			if {_a} is 0:
				send " &c&l» &r&3You do not have any of this item." to {_p}
			else:
				send " &b&l» &r&3+ &6%{_nw}% &3NW &b; &3New balance: &6%{nw::players::%{_p}%}%&b." to {_p}

function addNWItems(p: player, a: number, i: item):
	# Determines the amount of usable inventory space
	set {_usable_space} to 0
	set {_empty_slots} to 36
	loop items in {_p}'s inventory:
		remove 1 from {_empty_slots}
		if (loop-item is brick):
			add (64 - amount of loop-slot) to {_usable_space}
	add ({_empty_slots} * 64) to {_usable_space}

	# Determine the value of the item
	if {_i} is {nw::items::ckelp}:
		set {_value} to ({@kelp_value} * 4)
	else if {_i} is {nw::items::credit}:
		set {_value} to {@credit_value}
	else if {_i} is a dried kelp block:
		set {_value} to {@kelp_value}
	else if {_i} is a sea pickle:
		set {_value} to {@pickle_value}
	else:
		send "&cInvalid Item. If you get this error, please report it to a staff member." to {_p}
		stop

	# Max Withdrawals
	if {_a} is 0:
		set {_a} to floor({nw::players::%{_p}%} / ({_value}))

	# Complete the transaction
	if {nw::players::%{_p}%} is less than ({_a} * {_value}):
		send "&cYou do not have enough Net Worth to perform this action." to {_p}
	else:
		if {_usable_space} is 0:
			send "&cInsufficient inventory space." to {_p}
			stop
		if {_usable_space} < {_a}:
			set {_a} to {_usable_space} # If player has insufficient space, give the max possible.
			send "&cYou do not have enough inventory space to complete the full transaction." to {_p}
		remove ({_a} * {_value}) from {nw::players::%{_p}%}
		give {_p} {_a} of {_i}
		logc("Gave %{_a}% of %{_i}% to %{_p}% for %{_a} * {_value}% nw. Now %{nw::players::%{_p}%}%")
		send " &b&l» &r&3+ &9%{_a}% &3Items &b; &3- &6%{_a} * {_value}% &3NW &b; &3New balance: &6%{nw::players::%{_p}%}%&b." to {_p}

# unused
# function clearNWItems(p: player):
# 	set {_v} to getInventoryValue({_p})
# 	add {_v} to {nw::players::%{_p}%}

# 	remove all dried kelp blocks from {_p}'s inventory
# 	remove all sea pickles from {_p}'s inventory
# 	remove all {nw::items::credit} from {_p}'s inventory
# 	remove all {nw::items::ckelp} from {_p}'s inventory

# 	send "&3Added &6%{_v}% &3to your Net Worth." to {_p}
# 	send "&3Your Net Worth is now &6%{nw::players::%{_p}%}%&3." to {_p}

function networthGUI(p: player):
	set {_gui} to chest inventory with 5 rows named "Convert Items"

	set {_kamt} to (the amount of dried kelp blocks in {_p}'s inventory)
	set {_pamt} to (the amount of sea pickles in {_p}'s inventory)
	set {_ckamt} to (the amount of {nw::items::ckelp} in {_p}'s inventory)
	set {_camt} to (the amount of {nw::items::credit} in {_p}'s inventory)

	set {_total} to getInventoryValue({_p})

	set {_max::kelp} to floor({nw::players::%{_p}%} / ({@kelp_value}))
	set {_max::pickles} to floor({nw::players::%{_p}%} / ({@pickle_value}))
	set {_max::ckelp} to floor({nw::players::%{_p}%} / ({@kelp_value} * 4))
	set {_max::credits} to floor({nw::players::%{_p}%} / ({@credit_value}))

	set slot (integers between 0 and 45) of {_gui} to lime stained glass pane named " "
	set slot (integers between 0 and 4) and (integers between 9 and 12) and (integers between 18 and 21) and (integers between 27 and 30) and (integers between 36 and 39) of {_gui} to red stained glass pane named " "
	set slot 4 and 13 and 22 and 31 and 40 of {_gui} to light blue stained glass pane named " "
	# Important Things
	set slot 13 of {_gui} to the skull of {_p} named "&6%name of {_p}%" with lore " &b&l»&r &3Net Worth: &6%{nw::players::%{_p}%}%"
	set slot 31 of {_gui} to paper named "&c&lDEPOSIT &r&3| &l&bEVERYTHING" with lore " &b&l»&r &3Inventory Value: &6%{_total}%"
	# Deposits
	set slot 10 of {_gui} to dried kelp block named "&c&lDEPOSIT &r&3| &2Dried Kelp Block" with lore " &b&l»&r &3Left Click: Deposit &b&lALL" and " &b└ &6%{_kamt}%&3 for &6%{_kamt} * {@kelp_value}%&3 NW" and " " and " &b&l»&r &3Right Click: Deposit &b&l1" and " &b└ &3For &6%1 * {@kelp_value}%&3 NW"	
	set slot 11 of {_gui} to sea pickle named "&c&lDEPOSIT &r&3| &3Sea Pickles" with lore " &b&l»&r &3Left Click: Deposit &b&lALL" and " &b└ &6%{_pamt}%&3 for &6%{_pamt} * {@pickle_value}%&3 NW" and " " and " &b&l»&r &3Right Click: Deposit &b&l1" and " &b└ &3For &6%1 * {@pickle_value}%&3 NW"
	set slot 28 of {_gui} to dried kelp block of mending 10 named "&c&lDEPOSIT &r&3| &2Compressed Kelp" with lore " &b&l»&r &3Left Click: Deposit &b&lALL" and " &b└ &6%{_ckamt}%&3 for &6%{_ckamt} * {@kelp_value} * 4%&3 NW" and " " and " &b&l»&r &3Right Click: Deposit &b&l1" and " &b└ &3For &6%4 * {@kelp_value}%&3 NW"	
	set slot 29 of {_gui} to amethyst block of mending 10 named "&c&lDEPOSIT &r&3| &6Credits" with lore " &b&l»&r &3Left Click: Deposit &b&lALL" and " &b└ &6%{_camt}%&3 for &6%{_camt} * {@credit_value}%&3 NW" and " " and " &b&l»&r &3Right Click: Deposit &b&l1" and " &b└ &3For &6%1 * {@credit_value}%&3 NW"

	# Withdrawals
	set slot 15 of {_gui} to dried kelp block named "&a&lWITHDRAW &r&3| &2Dried Kelp Block" with lore " &b&l»&r &3Left Click: Withdraw &b&lMAX" and " &b└ &6%{_max::kelp}%&3 for &6%{_max::kelp} * {@kelp_value}%&3 NW" and " " and " &b&l»&r &3Right Click: Withdraw &b&l1" and " &b└ &3For &6%1 * {@kelp_value}%&3 NW"
	set slot 16 of {_gui} to sea pickle named "&a&lWITHDRAW &r&3| &3Sea Pickles" with lore " &b&l»&r &3Left Click: Withdraw &b&lMAX" and " &b└ &6%{_max::pickles}%&3 for &6%{_max::pickles} * {@pickle_value}%&3 NW" and " " and " &b&l»&r &3Right Click: Withdraw &b&l1" and " &b└ &3For &6%1 * {@pickle_value}%&3 NW"
	set slot 33 of {_gui} to dried kelp block of mending 10 named "&a&lWITHDRAW &r&3| &2Compressed Kelp" with lore " &b&l»&r &3Left Click: Withdraw &b&lMAX" and " &b└ &6%{_max::ckelp}%&3 for &6%{_max::ckelp} * {@kelp_value} * 4%&3 NW" and " " and " &b&l»&r &3Right Click: Withdraw &b&l1" and " &b└ &3For &6%4 * {@kelp_value}%&3 NW"
	set slot 34 of {_gui} to amethyst block of mending 10 named "&a&lWITHDRAW &r&3| &6Credits" with lore " &b&l»&r &3Left Click: Withdraw &b&lMAX" and " &b└ &6%{_max::credits}%&3 for &6%{_max::credits} * {@credit_value}%&3 NW" and " " and " &b&l»&r &3Right Click: Withdraw &b&l1" and " &b└ &3For &6%1 * {@credit_value}%&3 NW"
	
	open {_gui} to {_p}

on inventory click:
	name of event-inventory is "Convert Items"
	cancel event
	event-inventory is not player's inventory
	# Important
	if clicked slot is 31: # Paper - Withdraw All
		set {_v} to getInventoryValue(player)

		if {_v} = 0:
			networthGUI(player)
			send " &c&l» &r&3The value of your inventory is 0&c."
		else:
			set {_ck} to the amount of {nw::items::ckelp} in player's inventory # first to prevent confusion between k and ck
			set {_k} to (the amount of dried kelp blocks in player's inventory) - {_ck}
			set {_p} to the amount of sea pickles in player's inventory
			set {_c} to the amount of {nw::items::credit} in player's inventory
			logc("Depositing all;")
			removeItems(player, {_ck}, {nw::items::ckelp}, no) # first to prevent confusion between k and ck
			removeItems(player, {_k}, dried kelp block, no)
			removeItems(player, {_p}, sea pickle, no)
			removeItems(player, {_c}, {nw::items::credit}, no)
			networthGUI(player)
			set {_t} to {_k} + {_p} + {_ck} + {_c}
			if {_t} = 1:
				send " &b&l» &r&3+ &6%{_v}% &3NW &b; &3- &9%{_t}% &3Item &b; &3New balance: &6%{nw::players::%player%}%&b."
			else:
				send " &b&l» &r&3+ &6%{_v}% &3NW &b; &3- &9%{_t}% &3Items &b; &3New balance: &6%{nw::players::%player%}%&b."


	# Deposits
	else if clicked slot is 10: # Kelp
		if click type is left mouse button:
			removeItems(player, the amount of dried kelp blocks in player's inventory, dried kelp block, yes)
		else:
			removeItems(player, 1, dried kelp block, yes)
		networthGUI(player)
	else if clicked slot is 11: # Pickles
		if click type is left mouse button:
			removeItems(player, the amount of sea pickles in player's inventory, sea pickle, yes)
		else:
			removeItems(player, 1, sea pickle, yes)
		networthGUI(player)
	else if clicked slot is 28: # CKelp
		if click type is left mouse button:
			removeItems(player, the amount of {nw::items::ckelp} in player's inventory, {nw::items::ckelp}, yes)
		else:
			removeItems(player, 1, {nw::items::ckelp}, yes)
		networthGUI(player)
	else if clicked slot is 29:
		if click type is left mouse button: # Credits
			removeItems(player, the amount of {nw::items::credit} in player's inventory, {nw::items::credit}, yes)
		else:
			removeItems(player, 1, {nw::items::credit}, yes)
		networthGUI(player)
	# Withdrawals
	else if clicked slot is 15: # Kelp
		if click type is left mouse button:
			addNWItems(player, 0, dried kelp block)
		else:
			addNWItems(player, 1, dried kelp block)
		networthGUI(player)
	else if clicked slot is 16: # Pickles
		if click type is left mouse button:
			addNWItems(player, 0, sea pickle)
		else:
			addNWItems(player, 1, sea pickle)
		networthGUI(player)
	else if clicked slot is 33: # CKelp
		if click type is left mouse button:
			addNWItems(player, 0, {nw::items::ckelp})
		else:
			addNWItems(player, 1, {nw::items::ckelp})
		networthGUI(player)
	else if clicked slot is 34: # Credits
		if click type is left mouse button:
			addNWItems(player, 0, {nw::items::credit})
		else:
			addNWItems(player, 1, {nw::items::credit})
		networthGUI(player)

command /convert:
	aliases: /cv
	trigger:
		networthGUI(player)
		
command /nw [<text>] [<offline player>] [<number>]: # operator, amount, target
	aliases: /worth, /networth
	trigger:
		if arg-1 is "-h" or "help" or "-i" or "info":
			send ""
			send " &b| &3About &6Net Worth&b |&3 ----------------"
			send ""
			send " &b&l» &r&3Networth can be Withdrawn and Deposited using /convert (or &3/cv&3)."
			send " &b┌ &2Dried Kelp Blocks&b (&3Equal to &6%1 *{@kelp_value}% NW&b)&3," 
			send " &b├ &3Sea Pickles&b (&3Equal to &6%1 * {@pickle_value}% NW&b)&3,"
			send " &b├ &2Compressed Kelp&b (&3Equal to &6%4 * {@kelp_value}% NW&b)&3."
			send " &b├ &6Credits&b (&3Equal to &6%1 * {@credit_value}% NW&b)&3."
			send " &b└─ &3In your inventory and ender chest."
			send " &b&l» &r&3Networth is based in Netherite Ingots&3."
			send " &b&l» &r&3You can do &b/nw&3, &b/networth&3, or &b/worth&3 to get your networth&3."
			send " &b&l» &r&3Use &b/nw send <target> <amount>&3 to send net worth to another player&3."
			send ""
			send " &b| &3About &6Net Worth&b |&3 ----------------"
			send ""
			if player has permission "skript.nwadmin":
				send " &b| &3Admin &6Net Worth&3 Help&b |&3 ----------------"
				send ""
				send " &b&l» &r&3Usage: &9/nw <operator> <target> <amount>"
				send ""
				send " &bPossible Operators:"
				send " &b┌ ?&3, &bget&3, or&b query &6&l»&r&3 Returns the balance of the target."
				send " &b├ +&3,&b add&3,&b sum&3, or&b plus &6&l»&r&3 Adds the specified value."
				send " &b├ -&3,&b sub&3,&b diff&3, or&b minus &6&l»&r&3 Subtracts the specified value."
				send " &b└ =&3, or&b set&3 &6&l»&r&3 Sets the balance of the target to the specified value."
				send ""
				send " &b| &3Admin &6Net Worth&3 Help&b |&3 ----------------"
		else if arg-1 is "gui":
			networthGUI(player)
		else if arg-1 is set:
			set {_uuid} to arg-2's uuid
			if player has permission "skript.nwadmin":
				if arg-1 is "get" or "query" or "?":
					send "&3Player &b%arg-2%&3 has &6%{nw::players::%arg-2%}%&3 Net Worth."

				else if arg-1 is "add" or "+" or "sum" or "plus":
					send "&3Added &6%arg-3% &3NW to the balance of &b%arg-2%&3."
					send "&3Their old balance: &6%{nw::players::%arg-2%}%&3."
					add arg-3 to {nw::players::%arg-2%}
					send "&3Their new balance: &6%{nw::players::%arg-2%}%&3."
					logc("%player%: + %arg-3% nw to %arg-2%. Now %{nw::players::%arg-2%}%")

				else if arg-1 is "minus" or "-" or "difference" or "diff" or "remove" or "subtract" or "sub":
					send "&3Removed &6%arg-3% &3NW from the balance of &b%arg-2%&3."
					send "&3Their old balance: &6%{nw::players::%arg-2%}%&3."
					remove arg-3 from {nw::players::%arg-2%}
					send "&3Their new balance: &6%{nw::players::%arg-2%}%&3."
					logc("%player%: - %arg-3% nw from %arg-2%. Now %{nw::players::%arg-2%}%")

				else if arg-1 is "set" or "=":
					send "&3Set the balance of &b%arg-2%&3 to &6%arg-3%&3."
					send "&3Their old balance: &6%{nw::players::%arg-2%}%&3."
					logc("%player%: Set nw of %arg-2% to %arg-3%. Prev. %{nw::players::%arg-2%}%")
					set {nw::players::%arg-2%} to arg-3

				else if arg-1 is "send" or "gift": # Only works for online recipients
					if arg-2 is not online:
						send "  &c&l»&r &b%arg-2% &3is offline."
						stop
					if arg-3 = 0:
						send "  &c&l»&r &3Please specify a value greater than 0."
						stop
					if arg-3 > {nw::players::%player%}:
						send "  &c&l»&r &3You do not have enough net worth to complete this transaction."
						stop
					send "  &b&l»&r &3You are about to send &6%arg-3%&3 nw to &b%arg-2%&3. Your balance will be &6%{nw::players::%player%} - arg-3%&3 after the transaction."
					send formatted "<tooltip:Click here!><cmd:/nw cs %arg-2% %arg-3%>&bClick here to confirm."

				else if arg-1 is "csend" or "confirmsend" or "cs":
					remove arg-3 from {nw::players::%player%}
					add arg-3 to {nw::players::%arg-2%}
					send "  &b&l»&r &3Sent &6%arg-3%&3 nw to &b%arg-2%&3. You now have &6%{nw::players::%player%}% &3nw."
					logc("Transferred %arg-3% nw from %player% to %arg-2%. They now have {nw::players::%player%} (sender) and {nw::players::%arg-2%} (recipient) nw.")
				else:
					send "&cUnsupported Operator."
			else:
				send "&cInsufficient Permissions."
		else:
			send "&3Your Net Worth is &6%{nw::players::%player%}%&3."
			send "&3Useful Commands: &b/convert&3, &b/nw help&3, &b/nw send&3."
				
command /gift <number> <player> [<text>]:
	usage: /gift <amount> <recipient>
	trigger:
		if arg-2 is not online:
			send "  &c&l»&r &b%arg-2% &3is offline."
			stop
		if arg-1 = 0:
			send "  &c&l»&r &3Please specify a value greater than 0."
			stop
		if arg-1 > {nw::players::%player%}:
			send "  &c&l»&r &3You do not have enough net worth to complete this transaction."
			stop
		if arg-3 is "confirm" or "-c":
			remove arg-1 from {nw::players::%player%}
			add arg-1 to {nw::players::%arg-2%}
			send "  &b&l»&r &3Sent &6%arg-1%&3 nw to &b%arg-2%&3. You now have &6%{nw::players::%player%}% &3nw."
			logc("Transferred %arg-1% nw from %player% to %arg-2%. They now have {nw::players::%player%} (sender) and {nw::players::%arg-2%} (recipient) nw.")
		else:
			send "  &b&l»&r &3You are about to send &6%arg-1%&3 nw to &b%arg-2%&3. Your balance will be &6%{nw::players::%player%} - arg-1%&3 after the transaction."
			send formatted "<tooltip:Click here!><cmd:/gift %arg-1% %arg-2% -c>&bClick here to confirm."
	

### RANKS ###
function setRank(p: offline player, r: text):
	make console execute "/lp user %{_p}% parent set %{_r}%"
	logc("Updated rank of %{_p}% to %{_r}%.")

function removeRank(p: offline player):
	make console execute "/lp user %{_p}% parent set default"
	logc("Removed rank of %{_p}%.")

command /rank <text> <offline player> [<text>]:
	trigger:
		set {_t} to arg-2 # target player
		set {_tr} to arg-3 # target rank
		set {_t.cr} to (the luckperms group of {_t}) # target player's current rank
		set {_p.cr} to (the luckperms group of player) # player (user)'s current rank
		set {_moderated_ranks::*} to "owner" and "co-owner" and "head-mod" and "senior-mod" and "mod" and "trialmod" and "trainee-mod" and "op" and "trial-builder" and "builder" and "head-builder" and "admin" and "head-admin"

		if arg-1 is "get" or "query" or "?":
			if player has permission "skript.getrank":
				send "&6%{_t}%&3's rank is &6%{_t.cr}%&3, with the prefix &6'&r%the luckperms prefix of {_t}%&6'&3."
			else:
				send "&cInsufficient Permissions."
		else if arg-1 is "set" or "change" or "=" or "==":
			if player has permission "skript.addranks":
				loop {_moderated_ranks::*}:
					if {_t.cr} contains loop-value: # Prevents staff ranks from being overriden
						send "&cYou cannot set &6%{_t}%&c's rank."
						stop
					if {_tr} contains loop-value: # Prevents staff ranks from being given
						send "&cYou cannot set &6%{_t}%&c's rank to that value."
						stop
				setRank({_t}, {_tr})
				wait 1 second
				send "&3Successfully set the rank of &6%{_t}%&3 to &6%{_tr}%&3."
			else:
				send "&cInsufficient Permissions."
		else if arg-1 is "remove" or "delete" or "x":
			if player has permission "skript.removeranks":
				loop {_moderated_ranks::*}:
					if {_r} contains loop-value: # Prevents staff ranks from being overriden
						send "&cYou cannot set &6%{_t}%&c's rank."
						stop
				setRank({_t}, "default")
				send "&3Successfully removed the rank of &6%{_t}%&3."
			else:
				send "&cInsufficient Permissions."
				send "&cUse &b/ranks &cfor a list of ranks."


function ranksGuiMain(p: player):
	set {_gui} to chest inventory with 3 rows named "§eRanks Menu"

	set slot (integers between 0 and 26) of {_gui} to blue stained glass pane named " "
	set slot 8 of {_gui} to barrier block named "&cClear Rank"
	set slot 11 of {_gui} to netherite sword named "&cKill &3Ranks&r" with lore "&3Awarded to players with the required number of &cPlayer Kills&r"
	enchant slot 11 of {_gui} with sharpness 10
	set slot 13 of {_gui} to command block named "&eStaff &3Ranks&r" with lore "&3Obtainable by applying on our Discord."
	enchant slot 13 of {_gui} with mending 10
	set slot 15 of {_gui} to dried kelp block named "&6Networth &3Ranks&r" with lore "&3Awarded to players with a certain amount of &6Net Worth&r"
	enchant slot 15 of {_gui} with fortune 10
	open {_gui} to {_p}

function ranksGuiKills(p: player):
	set {_gui} to chest inventory with 3 rows named "§cKills Ranks"
	## SOME OF THESE COLOURS ARE PROBABLY RIGHT
	set slot (integers between 0 and 26) of {_gui} to blue stained glass pane named " "
	set slot 8 of {_gui} to barrier block named "&cClear Rank"
	if {kills::players::%{_p}%} is less than {k-ranks::murderer::req}:
		set slot 10 of {_gui} to wooden sword named "&31. &cMurderer&r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::murderer::req}% &3Kills" and " &b&l»&r &3Kills Left: &c%{k-ranks::murderer::req} - {kills::players::%{_p}%}%"
	else:
		set slot 10 of {_gui} to wooden sword of sharpness 10 named "&31. &cMurderer&r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::murderer::req}% &3Kills" and " &b&l»&r &9Click to Equip"
	
	if {kills::players::%{_p}%} is less than {k-ranks::genocidal-maniac::req}:
		set slot 11 of {_gui} to stone sword named "&32.<#983040> Genocidal Maniac &r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::genocidal-maniac::req}% &3Kills" and " &b&l»&r &3Kills Left: &c%{k-ranks::genocidal-maniac::req} - {kills::players::%{_p}%}%"
	else:
		set slot 11 of {_gui} to stone sword of sharpness 10 named "&32.<#983040> Genocidal Maniac &r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::genocidal-maniac::req}% &3Kills" and " &b&l»&r &9Click to Equip"
	
	if {kills::players::%{_p}%} is less than {k-ranks::sadistic::req}:
		set slot 12 of {_gui} to golden sword named "&33. &r &dSadistic&r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::sadistic::req}% &3Kills" and " &b&l»&r &3Kills Left: &c%{k-ranks::sadistic::req} - {kills::players::%{_p}%}%"
	else:
		set slot 12 of {_gui} to golden sword of sharpness 10 named "&33. &dSadistic&r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::sadistic::req}% &3Kills" and " &b&l»&r &9Click to Equip"

	if {kills::players::%{_p}%} is less than {k-ranks::demon::req}:
		set slot 14 of {_gui} to iron sword named "&34.<#610000> Demon&r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::demon::req}% &3Kills" and " &b&l»&r &3Kills Left: &c%{k-ranks::demon::req} - {kills::players::%{_p}%}%"
	else:
		set slot 14 of {_gui} to iron sword of sharpness 10 named "&34.<#610000> Demon&r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::demon::req}% &3Kills" and " &b&l»&r &9Click to Equip"
	
	if {kills::players::%{_p}%} is less than {k-ranks::demon::req}:
		set slot 15 of {_gui} to diamond sword named "&35.&5 Reaper&r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::reaper::req}% &3Kills" and " &b&l»&r &3Kills Left: &c%{k-ranks::reaper::req} - {kills::players::%{_p}%}%"
	else:
		set slot 15 of {_gui} to diamond sword of sharpness 10 named "&35.&5 Reaper&r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::reaper::req}% &3Kills" and " &b&l»&r &9Click to Equip"

	if {kills::players::%{_p}%} is less than {k-ranks::blood-god::req}:
		set slot 16 of {_gui} to netherite sword named "&36. &6Blood God&r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::blood-god::req}% &3Kills" and " &b&l»&r &3Kills Left: &c%{k-ranks::blood-god::req} - {kills::players::%{_p}%}%"
	else:
		set slot 16 of {_gui} to netherite sword of sharpness 10 named "&36. &6Blood God&r" with lore " &b&l»&r &3Requirement: &c%{k-ranks::blood-god::req}% &3Kills" and " &b&l»&r &9Click to Equip"
	set slot 26 of {_gui} to feather named "&cBack&r"
	open {_gui} to {_p}

function ranksGuiNetworth(p: player):
	set {_gui} to chest inventory with 3 rows named "§6Networth Ranks"
	## SOME OF THESE COLOURS ARE PROBABLY RIGHT
	set slot (integers between 0 and 26) of {_gui} to blue stained glass pane named " "
	set slot 8 of {_gui} to barrier block named "&cClear Rank"
	if {nw::players::%{_p}%} is less than {nw-ranks::aristocrat::req}:
		set slot 11 of {_gui} to dried kelp named "&31. &3Aristocrat&r" with lore " &b&l»&r &3Requirement: &6%{nw-ranks::aristocrat::req}% &3Net Worth" and " &b&l»&r &3Net Worth Left: &6%{nw-ranks::aristocrat::req} - {nw::players::%{_p}%}%"
	else:
		set slot 11 of {_gui} to dried kelp of fortune 10 named "&31. &3Aristocrat&r" with lore " &b&l»&r &3Requirement: &6%{nw-ranks::aristocrat::req}% &3Net Worth" and " &b&l»&r &9Click to Equip"
	if {nw::players::%{_p}%} is less than {nw-ranks::huge-aristocrat::req}:
		set slot 13 of {_gui} to dried kelp block named "&32. &aHuge Aristocrat&r" with lore " &b&l»&r &3Requirement: &6%{nw-ranks::huge-aristocrat::req}% &3Net Worth" and " &b&l»&r &3Net Worth Left: &6%{nw-ranks::huge-aristocrat::req} - {nw::players::%{_p}%}%"
	else:
		set slot 13 of {_gui} to dried kelp block of fortune 10 named "&32. &aHuge Aristocrat&r" with lore " &b&l»&r &3Requirement: &6%{nw-ranks::huge-aristocrat::req}% &3Net Worth" and " &b&l»&r &9Click to Equip"
	if {nw::players::%{_p}%} is less than {nw-ranks::ultimate-aristocrat::req}:
		set slot 15 of {_gui} to sea pickle named "&33. &5Ultimate Aristocrat&r" with lore " &b&l»&r &3Requirement: &6%{nw-ranks::ultimate-aristocrat::req}% &3Net Worth" and " &b&l»&r &3Net Worth Left: &6%{nw-ranks::ultimate-aristocrat::req} - {nw::players::%{_p}%}%"
	else:
		set slot 15 of {_gui} to sea pickle of fortune 10 named "&33. &5Ultimate Aristocrat&r" with lore " &b&l»&r &3Requirement: &6%{nw-ranks::ultimate-aristocrat::req}% &3Net Worth" and " &b&l»&r &9Click to Equip"
	set slot 26 of {_gui} to feather named "&cBack&r"
	open {_gui} to {_p}

function ranksGuiAdmins(p: player):
	set {_gui} to chest inventory with 3 rows named "§eAdmin Ranks"
	## IDK IF ANY OF THESE COLOURS ARE RIGHT
	set slot (integers between 0 and 26) of {_gui} to blue stained glass pane named " "
	set slot 8 of {_gui} to barrier block named "&cClear Rank"
	set slot 10 of {_gui} to hopper named "&5Trainee Mod&r" with lore "&3Recently appointed mod. Has been a player for 4+ weeks"
	set slot 11 of {_gui} to structure block named "&bTrial Mod&r" with lore "&3Has been a player for 2+ months. Trustworthy"
	set slot 12 of {_gui} to command block named "&aMod&r" with lore "&3Has been a trial mod for 2+ months. Very Trustworthy."
	set slot 14 of {_gui} to chain command block named "&3Senior Mod&r" with lore "&3Has been a mod for 6 months. Highly Trustworthy."
	set slot 15 of {_gui} to repeating command block named "&1Head Mod&r" with lore "&3Most trusted mod. Handpicked by the owner"
	set slot 16 of {_gui} to enchanted golden apple named "&cOwner&r" with lore "&3Has absolute power & authority."
	set slot 26 of {_gui} to feather named "&cBack&r"
	open {_gui} to {_p}

on inventory click: #TODO
	if name of event-inventory is "§eRanks Menu":
		cancel event
		event-inventory is not player's inventory
		if clicked slot is 8:
			removeRank(player)
			send "&3Cleared your rank."
		else if clicked slot is 11:
			ranksGuiKills(player)
		else if clicked slot is 13:
			ranksGuiAdmins(player)
		else if clicked slot is 15:
			ranksGuiNetworth(player)

	else if name of event-inventory is "§cKills Ranks":
		cancel event
		event-inventory is not player's inventory
		set {_k} to {kills::players::%player%}

		if clicked slot is 8:
			removeRank(player)
			send "&3Cleared your rank."
		else if clicked slot is 10: #murderer
			if {_k} is more than or equal to {k-ranks::murderer::req}:
				setRank(player, "murderer")
				send "&3Set your rank to &cMurderer&3."
		else if clicked slot is 11: # gm
			if {_k} is more than or equal to {k-ranks::genocidal-maniac::req}:
				setRank(player, "genocidal-maniac")
				send "&3Set your rank to &cGenocidal Maniac&3."
		else if clicked slot is 12: # sad
			if {_k} is more than or equal to {k-ranks::sadistic::req}:
				setRank(player, "sadistic")
				send "&3Set your rank to &cSadistic&3."
		else if clicked slot is 14: # demon
			if {_k} is more than or equal to {k-ranks::demon::req}:
				setRank(player, "demon")
				send "&3Set your rank to &cDemon&3."
		else if clicked slot is 15: # reaper
			if {_k} is more than or equal to {k-ranks::reaper::req}:
				setRank(player, "reaper")
				send "&3Set your rank to &cReaper&3."
		else if clicked slot is 16: # bg
			if {_k} is more than or equal to {k-ranks::blood-god::req}:
				setRank(player, "blood-god")
				send "&3Set your rank to &cBlood God&3."

		if clicked slot is 26:
			ranksGuiMain(player)
		else:
			ranksGuiKills(player)

	else if name of event-inventory is "§6Networth Ranks":
		cancel event
		event-inventory is not player's inventory

		set {_nw} to {nw::players::%player%}

		if clicked slot is 8:
			removeRank(player)
			send "&3Cleared your rank."
		else if clicked slot is 11:
			if {_nw} is more than or equal to {nw-ranks::aristocrat::req}:
				setRank(player, "aristocrat")
				send "&3Set your rank to &6Aristocrat&3."
		else if clicked slot is 13:
			if {_nw} is more than or equal to {nw-ranks::huge-aristocrat::req}:
				setRank(player, "huge-aristocrat")
				send "&3Set your rank to &6Huge Aristocrat&3."
		else if clicked slot is 15:
			if {_nw} is more than or equal to {nw-ranks::ultimate-aristocrat::req}:
				setRank(player, "ultimate-aristocrat")
				send "&3Set your rank to &6Ultimate Aristocrat&3."

		if clicked slot is 26:
			ranksGuiMain(player)
		else:
			ranksGuiNetworth(player)

	else if name of event-inventory is "§eAdmin Ranks":
		cancel event
		event-inventory is not player's inventory
		if clicked slot is 26:
			ranksGuiMain(player)

command /ranks:
	trigger:
		ranksGuiMain(player)
